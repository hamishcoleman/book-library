#!/usr/bin/perl
use warnings;
use strict;
#
# Given an ISBN, look it up
#

our $VERSION=1;

use Getopt::Long 2.33 qw(:config gnu_getopt);
use Pod::Usage;

use FileHandle;

use Data::Dumper;
$Data::Dumper::Indent = 1;
$Data::Dumper::Sortkeys = 1;

use XML::LibXML;

# FIXME - globals
my $parser = XML::LibXML->new();
my $db={};

###############################################################################
##
## Standard options processor
sub do_options(%@) {
	my $option = shift;
	GetOptions($option,@_) or pod2usage(2);
	pod2usage(-exitstatus => 0, -verbose => 2) if $option->{man};

	if ($option->{quiet}) {
		delete $option->{verbose};
	}
}

###############################################################################

my $option={};          # set option defaults here
$option->{verbose}=0;
$option->{key}='CHANGEME';
# remember to update the SYNOPSIS and OPTIONS when this changes
do_options($option,
		"man",
		"verbose|v+",
		"quiet|q",
		"key=s",
		"isbn=s",
		"xml=s",
		"stdin",
		"log=s",
		"db=s",
);

#
# Generic http dumping routine
#
sub dump_res($$) {
	my ($desc, $res) = @_;

	if (!$res->is_success) {
		print "DUMP RES: $desc\n";
		print "Status:", $res->status_line, "\n";
		print Dumper($res);
		warn "!is_success";
	}

	if ($option->{verbose}>1) {
		print "DUMP RES: $desc\n";
		print Dumper($res);
	}
}

#
# Setup the LWP useragent
#
sub http_setup() {
	use LWP::UserAgent;
	use HTTP::Cookies;
	my $ua = LWP::UserAgent->new;
	$ua->agent("Mozilla/5.0 (X11; U; Linux i686; en-US; rv:1.8.1.3) Gecko/20070310");
	$ua->cookie_jar(HTTP::Cookies->new());

	# LWP talks about RFC 2616 when it says that POST's cannot redirect
	# I have looked up the standard and it does not appear to say that
	#
	# FIXME - find out why LWP wants this in the face of the above comment
	push @{ $ua->requests_redirectable }, 'POST';

	return $ua;
}

sub isbn_xml($) {
	my ($text) = @_;
	my $res;

	my $doc = $parser->parse_string($text);
	if (!$doc) {
		warn "Not a valid XML document";
		return;
	}
	my $root = $doc->getDocumentElement();

	return $root;
}

#
# Make an isbndb.com query and return the XML result data
#
sub isbn_get($$$) {
	my ($ua,$key,$isbn) = @_;

	my $url = "http://isbndb.com/api/books.xml?access_key=$key&results=details,texts,prices,authors,keystats&index1=isbn&value1=$isbn";

	addlog('GET ',$url);
	my $req = HTTP::Request->new(GET => $url);
	my $res = $ua->request($req);
	dump_res('get',$res);

	if (!$res->is_success) {
		return;
	}
	addlog($res->content,"\n");
	$db->{_}{isbn_get}=$res->content;

	return isbn_xml($res->content);
}

sub isbn_xml2text($) {
	my ($root) = @_;
	my $res;

	for my $node ($root->getElementsByTagName('BookData')) {
		$res.=$node->getAttribute('isbn')."\t";
		for my $title ($node->getElementsByTagName('Title')) {
			$res.=$title->textContent().", ";
		}
		for my $title ($node->getElementsByTagName('AuthorsText')) {
			$res.=$title->textContent().", ";
		}
		$res.="\n";
	}

	return $res;
}

# <KeyStats granted="22" access_key="CHANGEME" requests="22" limit="500" />
sub isbndb_granted($) {
	my ($root) = @_;
	my $res;

	for my $node ($root->getElementsByTagName('KeyStats')) {
		$res = $node->getAttribute('granted');
	}
	return $res;
}

sub addlog {
	if (!$option->{log}) {
		return;
	}
	if (!$option->{logfh}) {
		$option->{logfh} = new FileHandle $option->{log},"a";
		if (!defined $option->{logfh}) {
			die "could not open $option->{log} $!\n";
		}
	}
	$option->{logfh}->print(@_);
}

sub adddb {
	if (!$option->{db}) {
		return;
	}

	my ($root) = @_;
	for my $node ($root->getElementsByTagName('BookData')) {
		$option->{db_isbndb}->appendChild($node);
	}

	# paranoid write
	my $fh = new FileHandle $option->{db}.'.new','w';
	if (!defined $fh) {
		die "could not open $option->{db}.new $!\n";
	}
	$option->{db_doc}->toFH($fh,1);
}

sub not_found_bookdata($$) {
	my ($isbn,$text) = @_;

	my $root = XML::LibXML::Element->new('ISBNdb');
	my $node = XML::LibXML::Element->new('BookData');
	$root->appendChild($node);
	$node->setAttribute('book_id','unkid'.$isbn);
	$node->setAttribute('isbn',$isbn);
	$node->appendTextChild('Title',$text);
	$node->appendTextChild('AuthorsText','UNKNOWN');
	$node->appendTextChild('Summary','');
	$node->appendTextChild('Details','');
	$node->appendTextChild('Prices','');
	$node->appendTextChild('Authors','');

	return $root;
}

sub do_ls {
	if (!$option->{db}) {
		return;
	}

	print isbn_xml2text($option->{db_isbndb});
}

if ( !defined($option->{key})){
	die "must specify access key";
}

if ($option->{db}) {
	my $fh = new FileHandle $option->{db},"r";
	if (!defined $fh) {
		die "could not open $option->{db} $!\n";
	}
	$option->{db_doc} = $parser->parse_fh($fh);
	# TODO - warn / create an empty document on fail
	$option->{db_isbndb} = $option->{db_doc}->getDocumentElement();
}

my $ua = http_setup();

if ($option->{xml}) {
	my $xml = $option->{xml};
	my $root = isbn_xml($xml);
	my $text = isbn_xml2text($root);
	if (!defined $text) {
		$text = "NOT FOUND\n";
	}
	print $text;
	addlog("NEWRECORD\n");	# a record separator
	addlog($xml,"\n");
	addlog("T xx\t$text");
	adddb($root);
	exit;
}

my $keystats = 0;
if ($option->{stdin}) {
	# prompt
	print "> ";
	my $prev_not_found;
	while(<>) {
		chomp;

		if ($_ =~ m/^[0-9]/) {
			# TODO - split input at first non digit
			print "$_\t";
			my $root = isbn_get($ua,$option->{key},$_);
			if ($root) {
				my $text = isbn_xml2text($root);
				if (!defined $text) {
					$text = "NOT FOUND\n";
					$prev_not_found = $_;
				}
				print $text;
				addlog("T $_\t$text");

				adddb($root);
				if (defined $prev_not_found) {
					print "P:$_ ";
				}
				print "G:",isbndb_granted($root);
			} else {
				print "ERROR: getting isbn details\n";
			}
		} elsif ($_ =~ m/^:prev (.*)/) {
			$prev_not_found = $1;
			print "P:$1";
		} elsif ($_ =~ m/^:ls ?(.*)/) {
			do_ls($1);
		} else {
			# some kind of manual entry
			addlog("NEWRECORD\n");	# a record separator
			addlog("M $_\n");
			if ($_ && $prev_not_found) {
				my $root = not_found_bookdata($prev_not_found,$_);
				my $text = isbn_xml2text($root);
				print $text;
				adddb($root);
			}
			undef $prev_not_found;
		}
		# prompt
		print "> ";
	}
} elsif (!defined($option->{isbn})) {
	die "Must specify isbn";
} else {
	my $root = isbn_get($ua,$option->{key},$option->{isbn});
	if ($root) {
		my $text = isbn_xml2text($root);
		if (!defined $text) {
			$text = "NOT FOUND\n";
		}
		print $text;
		addlog("T $_\t$text");
		adddb($root);
		print "G:",isbndb_granted($root);
		print "\n";
	} else {
		print "ERROR: getting isbn details\n";
	}
}

if ($option->{db}) {
	rename($option->{db},$option->{db}.'.old');
	rename($option->{db}.'.new',$option->{db});
}

__END__
